# GPLconx -- visualize 2-D hyperbolic geometry.
# Copyright (C) 1996-2001  David L. Chandler

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/conxv.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(GPLconx, 0.150)
dnl Grep for `GPLconx_VERSION', you must change here and in .cvsignore and
dnl in `bak.sh' if you have it.


AC_CONFIG_AUX_DIR(.)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_CXX
AC_PROG_CPP
dnl We need yacc/bison and lex/flex:
AC_PROG_YACC
AM_PROG_LEX

AC_DISABLE_SHARED
AM_PROG_LIBTOOL
AC_PROG_INSTALL
dnl AC_PROG_RANLIB

dnl src/ttalk-sh depends on `#!' support:
AC_SYS_INTERPRETER
AM_CONDITIONAL(WE_HAVE_SYS_INTERP, test "$interpval" = "yes")

dnl Checks for libraries.
AC_CHECK_LIB(m, sin)

dnl Checks for header files.
AC_HEADER_STDC
dnl DLC use these checks.
AC_CHECK_HEADERS(errno.h ctype.h stdio.h stdlib.h string.h assert.h unistd.h sys/stat.h time.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_C_INLINE


dnl Checks for library functions.  CHECK_LIB should be called first.
AC_REPLACE_FUNCS(acosh)

dnl Check for gengetopt-2.2's generated source's dependencies, but don't put
dnl them in LIBOBJS since those go into the library, and these go into tconx
dnl but not gconx.
dnl
dnl If you suspect that your getopt_long is present but not working correctly,
dnl then change getopt_long to getooopt_long, which will not be found, and
dnl you will use GPLconx's getopt and getopt_long.
AC_SUBST(GGOOBJS)
AC_CHECK_FUNCS(getopt_long , , [GGOOBJS="$GGOOBJS getopt.o getopt1.o"])

dnl GNU gengetopt-2.2 turns tconxopt.ggo into `.c' and `.h' files.
dnl DLC check version.
missing_dir=`cd $ac_aux_dir && pwd`
AM_MISSING_PROG(GENGETOPT, gengetopt, $missing_dir)

dnl If you have Mark J. Kilgard's GLUT, use this to tell idir such that
dnl $idir/GL/glut.h exists and ldir such that $ldir/libglut* exists.
dnl You still must independently enable GLUT.
AC_ARG_WITH(glut-incdir,
[  --with-glut-incdir[=DIR]   where to find GL/glut.h],
GLUTINCDIR=$withval
)
AC_ARG_WITH(glut-libdir,
[  --with-glut-libdir[=DIR]   where to find libglut*],
GLUTLIBDIR=$withval
)
AC_ARG_ENABLE(glut,
  [  --enable-glut   Make gconx, GPLconx with a primitive GLUT GUI.],
  [
    case "${enableval}" in
      yes) enableglut=true ;;
      no)  enableglut=false ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-glut]) ;;
    esac
  ],
  [ enableglut=false ]
)
if test "$enableglut" = "true"; then
        AC_CHECK_HEADER($GLUTINCDIR/GL/glut.h, GCONX=gconx, AC_MSG_ERROR($GLUTINCDIR/GL/glut.h was not found so do not use --enable-glut or fix --with-glut-incdir))
fi
AC_SUBST(GLUTLIBDIR)
AC_SUBST(GLUTINCDIR)
AC_SUBST(GCONX)

dnl tconx is outdated, so don't make it.  It does not have the Smalltalkish
dnl language.
AC_ARG_ENABLE(tconx,
  [  --enable-tconx   Make tconx, the outdated predecessor to cxxconx.],
  [
    case "${enableval}" in
      yes) enabletconx=true ;;
      no)  enabletconx=false ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-tconx]) ;;
    esac
  ],
  [ enabletconx=false ]
)
if test "$enabletconx" = "true"; then
        LIBCONXLA=libconx.la
        TCONX=tconx
fi
AC_SUBST(TCONX)
AC_SUBST(LIBCONXLA)

dnl Extensive gcc compiler warnings help ensure correctness.
AC_ARG_ENABLE(warn,
  [  --enable-warn   Enable compiler warnings if gcc is used],
  [
    case "${enableval}" in
      yes) compwarnings=true ;;
      no)  compwarnings=false ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-warn]) ;;
    esac
  ],
  [ compwarnings=false ]
)
FIRM_CFLAGS=""
if test x$compwarnings = xtrue; then
        if test "x$GCC" = xyes; then
                FIRM_CFLAGS="$FIRM_CFLAGS -Wall -Wtraditional -Wpointer-arith -Wno-parentheses -Wcast-qual -Wcast-align -Wnested-externs -Wmissing-prototypes -Wmissing-declarations -Wstrict-prototypes -Wwrite-strings"
        fi
dnl -Waggregate-return warnings will alert you to possible performance
dnl pitfalls.
dnl Don't bother with other compilers, that's what CFLAGS and CXXFLAGS are for.
fi

AC_SUBST(FIRM_CFLAGS)

dnl The maintainer has more fun since he has the `website/' directory from
dnl the CVS repository enabled.  Use --with-maint-user=dchandler
dnl or whatever your gplconx.sourceforge.net user name is.
AC_ARG_WITH(maint-user,
[  --with-maint-user[=USERNAME]   Enable maintainer-only features],
MAINTUSER=$withval
)
if test x$MAINTUSER != x; then
        if test x$MAINTUSER != xdchandler; then
                AC_MSG_ERROR([bad user name ${MAINTUSER} for --with-maint-user])
        fi
fi


dnl See section ``Autoconf `.o' macros'' in libtool manual (libtool.info*)
LTLIBOBJS=`echo "$LIBOBJS" | sed 's/\.o/.lo/g'`
AC_SUBST(LTLIBOBJS)
if test x$MAINTUSER = xdchandler; then
        MAINTSUBDIRS=website
        AC_LINK_FILES(README, website/README)
fi
AC_SUBST(MAINTSUBDIRS)
AC_OUTPUT(Makefile src/Makefile Togl/Makefile src/Tcl/Makefile doc/Makefile src/lcl/Makefile website/Makefile)

